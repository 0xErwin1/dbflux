# Maintainer: Ignacio Perez <ignacio@feuer.me>

pkgname=dbflux
pkgver=0.3.0
pkgrel=1
pkgdesc="A fast, keyboard-first database client"
arch=(x86_64 aarch64)
url="https://github.com/0xErwin1/dbflux"
license=('MIT' 'Apache-2.0')
depends=(
    'openssl'
    'sqlite'
    'zstd'
    'zlib'
    'fontconfig'
    'freetype2'
    'wayland'
    'libxkbcommon'
    'libx11'
    'libxcb'
    'libxcursor'
    'libxrandr'
    'libxi'
    'vulkan-icd-loader'
    'alsa-lib'
    'libgit2'
    'curl'
    'libsecret'
    'libssh2'
    'dbus'
    'tree-sitter'
)
makedepends=(
    'rust'
    'pkgconf'
    'cmake'
    'vulkan-headers'
    'git'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('SKIP')

build() {
    cd "${pkgname}-${pkgver}"

    export ZSTD_SYS_USE_PKG_CONFIG=1

    cargo build --release --locked --package dbflux --features sqlite,postgres,mysql,mongodb,redis
}

check() {
    cd "${pkgname}-${pkgver}"

    cargo test --release --locked --package dbflux --features sqlite,postgres,mysql,mongodb,redis
}

package() {
    cd "${pkgname}-${pkgver}"

    # Install binary
    install -Dm755 "target/release/dbflux" "${pkgdir}/usr/bin/dbflux"

    # Install desktop file
    install -Dm644 "resources/desktop/dbflux.desktop" "${pkgdir}/usr/share/applications/dbflux.desktop"
    # Replace placeholder with actual binary path
    sed -i "s|@EXEC_PATH@|/usr/bin/dbflux|g" "${pkgdir}/usr/share/applications/dbflux.desktop"

    # Install icon
    install -Dm644 "resources/icons/dbflux.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/dbflux.svg"

    # Install mime type
    install -Dm644 "resources/mime/dbflux-sql.xml" "${pkgdir}/usr/share/mime/packages/dbflux-sql.xml"

    # Install license
    install -Dm644 "LICENSE-MIT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT" 2>/dev/null || true
    install -Dm644 "LICENSE-APACHE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE" 2>/dev/null || true
}
